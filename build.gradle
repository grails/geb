buildscript {
    repositories {
        maven { url = 'https://repo.grails.org/grails/core' }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
    }
}

version = projectVersion
group = 'org.grails.plugins'

apply plugin: 'java-library'
apply plugin: 'java-test-fixtures'
apply plugin: 'org.grails.grails-plugin'
apply plugin: 'org.grails.internal.grails-plugin-publish'
apply plugin: 'maven-publish'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
        vendor = JvmVendorSpec.BELLSOFT
    }
}

repositories {
    maven { url = 'https://repo.grails.org/grails/core' }
}

configurations {
    documentation.extendsFrom compileClasspath
}

dependencies {

    implementation(platform("org.grails:grails-bom:$grailsVersion"))

    compileOnly 'org.grails:grails-core' // Provided as this is a Grails plugin

    testFixturesCompileOnly 'jakarta.servlet:jakarta.servlet-api'
    testFixturesApi 'org.gebish:geb-spock'
    testFixturesApi 'org.grails:grails-testing-support'
    testFixturesApi 'org.grails:grails-datastore-gorm'
    testFixturesApi "org.testcontainers:selenium:$testcontainersVersion"
    testFixturesApi "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
    testFixturesApi "org.seleniumhq.selenium:selenium-remote-driver:$seleniumVersion"

    documentation 'org.apache.groovy:groovy'
    documentation 'org.apache.groovy:groovy-ant'
    documentation 'org.apache.groovy:groovy-templates'
    documentation 'com.github.javaparser:javaparser-core'
}

grailsPublish {
    userOrg = 'grails'
    githubSlug = 'grails/geb'
    license {
        name = 'Apache-2.0'
    }
    title = 'Grails Geb Plugin'
    desc = 'Provides Integration with Geb for Functional Testing'
    developers = [
        graemerocher: 'Graeme Rocher',
        puneetbehl: 'Puneet Behl',
        sbglasius: 'SÃ¸ren Berg Glasius',
        matrei: 'Mattias Reichel'
    ]
}

tasks.withType(Groovydoc).configureEach {
    destinationDir = layout.buildDirectory.dir('docs/api').get().asFile
    docTitle = "Grails Geb Plugin ${version}"
    classpath = configurations.documentation
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
        showExceptions true
        exceptionFormat 'full'
        showCauses true
        showStackTraces true
    }
}

tasks.named('bootJar') { enabled = false }
tasks.named('bootRun') { enabled = false }
tasks.named('bootTestRun') { enabled = false }
tasks.named('findMainClass') { enabled = false }